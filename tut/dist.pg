# distortion plots
#
# build with `gnuplot dist.pg`


# Common

s = 0.5

# Hard Clipping

f(x) = (x > 1.0) ? 1.0 : (x < -1.0) ? -1 : x

set terminal svg size 400 300
set output 'fig/dist_hard_ramp.svg'

set yrange [0:4]
plot [0:4] x    title "input"  with lines linestyle 1, \
           f(x) title" output" with lines linestyle 2

set terminal svg size 400 300
set output 'fig/dist_hard_sine.svg'

set yrange [-2:2]
plot [0:2*pi]   2*sin(x)  title "input"  with lines linestyle 1, \
              f(2*sin(x)) title "output" with lines linestyle 2


# Linear Clipping


g(x,d) = (x < s) ? x : (s + 1/(1+d) * (x - s))
f(x,d) = x > 0 ? g(x,d) : -g(-x,d)

set terminal svg size 400 300
set output 'fig/dist_lin_ramp.svg'

set yrange [0:4]
plot [0:4] x        title "input"  with lines linestyle 1, \
           f(x,0.3) title "output" with lines linestyle 2, \
           f(x,1.0) title "output" with lines linestyle 3, \
           f(x,3.0) title "output" with lines linestyle 4

set terminal svg size 400 300
set output 'fig/dist_lin_sine.svg'

set yrange [-3:3]
plot [0:2*pi]   3*sin(x)  title "input"  with lines linestyle 1, \
              f(3*sin(x),0.3) title "output" with lines linestyle 2, \
              f(3*sin(x),1.0) title "output" with lines linestyle 3, \
              f(3*sin(x),3.0) title "output" with lines linestyle 4


# Polynomial Clipping


g(x,m) = (x < s) ? x : (x > 2*m-s) ? m : (0.25/(s-m) * ((x-2*m+s)**2) + m)
f(x,d) = x > 0 ? g(x,s+1/d) : -g(-x,s+1/d)

set terminal svg size 400 300
set output 'fig/dist_poly_ramp.svg'

set yrange [0:4]
plot [0:4] x        title "input"  with lines linestyle 1, \
           f(x,0.5) title "output" with lines linestyle 2, \
           f(x,1.0) title "output" with lines linestyle 3, \
           f(x,3.0) title "output" with lines linestyle 4

set terminal svg size 400 300
set output 'fig/dist_poly_sine.svg'

set yrange [-3:3]
plot [0:2*pi]   3*sin(x)  title "input"  with lines linestyle 1, \
              f(3*sin(x),0.3) title "output" with lines linestyle 2, \
              f(3*sin(x),1.0) title "output" with lines linestyle 3, \
              f(3*sin(x),3.0) title "output" with lines linestyle 4


# Root Clipping


g(x,d) = (x < s) ? x : 2 * sqrt(d * (x-s+d)) - 2*d + s
f(x,d) = x > 0 ? g(x,1/d) : -g(-x,1/d)

set terminal svg size 400 300
set output 'fig/dist_root_ramp.svg'

set yrange [0:4]
plot [0:4] x        title "input"  with lines linestyle 1, \
           f(x,0.5) title "output" with lines linestyle 2, \
           f(x,1.0) title "output" with lines linestyle 3, \
           f(x,3.0) title "output" with lines linestyle 4

set terminal svg size 400 300
set output 'fig/dist_root_sine.svg'

set yrange [-3:3]
plot [0:2*pi]   3*sin(x)  title "input"  with lines linestyle 1, \
              f(3*sin(x),0.3) title "output" with lines linestyle 2, \
              f(3*sin(x),1.0) title "output" with lines linestyle 3, \
              f(3*sin(x),3.0) title "output" with lines linestyle 4


# Logarithmic Clipping


g(x,d) = (x < s) ? x : d * log((x-s+d)/d) + s
f(x,d) = x > 0 ? g(x,1/d) : -g(-x,1/d)

set terminal svg size 400 300
set output 'fig/dist_log_ramp.svg'

set yrange [0:4]
plot [0:4] x        title "input"  with lines linestyle 1, \
           f(x,0.5) title "output" with lines linestyle 2, \
           f(x,1.0) title "output" with lines linestyle 3, \
           f(x,3.0) title "output" with lines linestyle 4

set terminal svg size 400 300
set output 'fig/dist_log_sine.svg'

set yrange [-3:3]
plot [0:2*pi]   3*sin(x)  title "input"  with lines linestyle 1, \
              f(3*sin(x),0.3) title "output" with lines linestyle 2, \
              f(3*sin(x),1.0) title "output" with lines linestyle 3, \
              f(3*sin(x),3.0) title "output" with lines linestyle 4
